buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "0.1"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            // save chatbox/build/outputs/mapping/release/mapping.txt to deobfuscate stacktraces
            // see https://developer.android.com/studio/build/shrink-code?hl=fr#decode-stack-trace
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.jakewharton:disklrucache:2.0.2'
}

// Publish to Bintray
// See https://medium.com/devnibbles/publishing-your-first-android-library-to-bintray-da08c8a76e1a
// Usage
// To distribute: in library folder, `run ./gradlew clean build bintrayUpload`
// To include, add these lines to build.gradle:
// If hosted in our own repo
//  repositories {
//      maven { url 'https://dl.bintray.com/com-sebastienvitard/com.sebastienvitard' }
//  }
//  dependencies {
//      implementation 'com.sebastienvitard:libpackagetest:0.1'
//  }
// If hosted in jcenter repo (if requested and accepted)
//  dependencies {
//      implementation 'com.sebastienvitard:libpackagetest:0.1'
//  }
def libraryGroupId = 'com.sebastienvitard'
def libraryArtifactId = 'libpackagetest'
def libraryVersion = '0.1'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = libraryVersion
            }
        }
    }
}

bintray {
    user='user'
    key='key'
    publications = ['release']
    publish = true
    override = true
    pkg {
        repo = libraryGroupId
        userOrg = 'com-sebastienvitard'
        name = libraryArtifactId
        desc = 'A test of lib packaging'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Doc1faux/LibPackageTest.git'
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            released = new Date()
        }
    }
}
